plugins {
  id 'org.hidetake.ssh' version '2.10.1'
}

ext {
  springBootVersion = '2.2.0.RELEASE'
}

repositories {
  mavenCentral()
    jcenter()
}

apply from: "${rootDir}/gradle/banner.gradle"
apply from: "${rootDir}/gradle/envInfo.gradle"

repositories {
  mavenCentral()
  maven {
    url "http://repo.spring.io/milestone"
  }
}

// start - check early and fail on dependency resolution during configuration
def afterEvaluation = false
gradle.projectsEvaluated {
    afterEvaluation = true
}

allprojects { project ->
  configurations.all { configuration ->
    configuration.incoming.beforeResolve {
      if (!afterEvaluation) {
        logger.warn("Configuration $configuration.name of project $project.name is being resolved at configuration time.")
      }
    }
  }
}
// end - check early and fail on dependency resolution during configuration

subprojects { sub ->
  sub.configurations.all {
    resolutionStrategy {
      failOnVersionConflict()
      force 'org.slf4j:slf4j-api:1.7.25'
      force 'org.checkerframework:checker-qual:2.10.0'
      force 'com.google.errorprone:error_prone_annotations:2.3.3'
      force 'org.springframework:spring-web:5.2.0.RELEASE'
      force 'org.springframework:spring-test:5.2.0.RELEASE'
      force 'org.springframework:spring-webmvc:5.2.0.RELEASE'
      force 'net.bytebuddy:byte-buddy:1.9.11'
      force 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
      force 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    }
  }
}

version = '0.1'

task testReport(type: TestReport, group: 'Build') {
  description = 'Generates an aggregate test report'
  destinationDir = file("${buildDir}/reports/allTests")
}

allprojects {
  apply plugin: 'jacoco'
  apply plugin: 'project-report'
}

apply from: "${rootDir}/gradle/subProjects.gradle"
apply from: "${rootDir}/gradle/deploy.gradle"

